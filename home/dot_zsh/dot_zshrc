if [ "$ZSHRC_PROFILE" != "" ]; then
  zmodload zsh/zprof && zprof > /dev/null
fi

function zsh-profiler() {
  ZSHRC_PROFILE=1 zsh -i -c zprof
}

setopt IGNOREEOF

autoload -Uz colors
colors

autoload -Uz compinit
compinit

bindkey -e

bindkey "^[[3~" delete-char
bindkey "^?" backward-delete-char
bindkey ";5D" backward-word
bindkey ";5C" forward-word

autoload -Uz select-word-style
select-word-style default
zstyle ':zle:*' word-chars ' /=;@:{}[]()<>,|.'
zstyle ':zle:*' word-style unspecified

setopt share_history
setopt histignorealldups
setopt hist_ignore_space

HISTFILE=$ZDOTDIR/.zsh_history
HISTSIZE=2000
SAVEHIST=2000

# Load common settings
if [ -z "$SHELL_COMMON" ]; then
  SHELL_COMMON="$HOME/.shell_common"
fi
[ -f "$SHELL_COMMON/interactive.sh" ] && source "$SHELL_COMMON/interactive.sh"

if [ "$HAS_STARSHIP" = "1" ]; then
  eval "$(starship init zsh)"
fi

if type fzf &> /dev/null; then
  function fzf-history-selection() {
    local selected=""

    if [[ -n "$LBUFFER" ]]; then
      selected=`history 0 | fzf --query $LBUFFER | awk '{ $1=""; sub(/^[ \t]+/, ""); print }'`
    else
      selected=`history 0 | fzf | awk '{ $1=""; sub(/^[ \t]+/, ""); print }'`
    fi

    BUFFER=`[ ${#selected} -gt 0 ] && echo $selected || echo $BUFFER`
    CURSOR=$#BUFFER
    zle reset-prompt
  }

  zle -N fzf-history-selection
  bindkey '^R' fzf-history-selection
fi

# zsh-specific FZF tab completion
if type fzf &> /dev/null && [ -f ~/.zsh/functions/fzf-tab-completion.zsh ]; then
  source ~/.zsh/functions/fzf-tab-completion.zsh
  bindkey '^I' fzf_completion
fi

[ -f "$ZDOTDIR/ni.zsh" ] && source $ZDOTDIR/ni.zsh

echo -ne "\033]0;$(basename $PWD)\007"
function chpwd() {
  echo -ne "\033]0;$(basename $PWD)\007"
}

# Aikido Safe Chain: wrap package managers with malware scanning
[ -f "$HOME/.safe-chain/scripts/init-posix.sh" ] && source "$HOME/.safe-chain/scripts/init-posix.sh"

if [ -e $HOME/.zshrc.local ]; then
    source $HOME/.zshrc.local
fi

if [[ "$TERM_PROGRAM" == "vscode" ]]; then
  if [[ "$TERM_PROGRAM_VERSION" == *"-insider" ]]; then
    . "$(code-insiders --locate-shell-integration-path zsh)"
  else
    . "$(code --locate-shell-integration-path zsh)"
  fi
fi

:
